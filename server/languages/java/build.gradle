apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'

buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}

def grpcVersion = '1.12.0'
dependencies {
	compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
	compile 'javax.annotation:javax.annotation-api:1.2'  //Fixes generated code compilation errors under Java 9+
	compile 'com.github.javaparser:javaparser-symbol-solver-core:3.6.7'
	compile 'com.google.api.grpc:proto-google-common-protos:1.11.0'
	compile "io.grpc:grpc-netty:${grpcVersion}"
	compile "io.grpc:grpc-protobuf:${grpcVersion}"
	compile "io.grpc:grpc-stub:${grpcVersion}"
}

//Copies the .proto files into the source tree
task copyProtos (type: Copy) {
	from '../../proto'
	into 'src/main/proto'
	include '*.proto'
}

//Ensure the .proto files are copied prior to compilation
compileJava.dependsOn copyProtos

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.5.1-1'
	}
	
	plugins
	{
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	
	generateProtoTasks
	{
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

jar {
	baseName = 'language-java'
	
	manifest {
		attributes 'Main-Class': 'Application'
	}
	
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

task deploy (dependsOn: jar, type: Copy) {
	from 'build/libs'
	into 'build/bin'
	include '*.jar'
}
